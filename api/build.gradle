plugins {
  id 'eclipse'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.13"
}

dependencies {
    implementation(
        project(":common"),
        project(":jersey"),
        project(":bhive"),
    )
    api(
        'io.swagger.core.v3:swagger-annotations:2.1.13',
    )
    swaggerDeps(
        'org.apache.commons:commons-lang3:3.12.0',
        'io.swagger.core.v3:swagger-jaxrs2-jakarta:2.1.13',
        'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0',
        'jakarta.servlet:jakarta.servlet-api:5.0.0'
    )
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

build.dependsOn shadowJar
build.dependsOn resolve

shadowJar {
	classifier = ''
    baseName = 'bdeploy-api'
}

resolve {
    outputFileName = 'BDeployAPI'
    outputFormat = 'YAML'
    classpath = sourceSets.main.runtimeClasspath
    outputDir = file("${buildDir}/openapi")
    readAllResources = false
    resourceClasses = [
        'io.bdeploy.api.remote.v1.PublicRootResource',
    ]
}

evaluationDependsOn(':bhive')
evaluationDependsOn(':common')
evaluationDependsOn(':jersey')

task allSourcesJar(type: Jar, dependsOn: classes) {
	group = 'build'
	classifier = 'sources'
	from files([project, project(":bhive"), project(":jersey"), project(":common")].collect { it.sourceSets.main.allSource })
}

task allJavadoc(type: Javadoc) {
    source files([project, project(":bhive"), project(":jersey"), project(":common")].collect { it.sourceSets.main.allJava })
    classpath = files([project, project(":bhive"), project(":jersey"), project(":common")].collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")

    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

task allJavadocJar(type: Jar, dependsOn: allJavadoc) {
    classifier = 'javadoc'
    from allJavadoc.destinationDir
}

artifacts {
	archives allSourcesJar
	archives allJavadocJar
}

shadowJar.dependsOn allSourcesJar
shadowJar.dependsOn allJavadocJar

publishing {
	publications {
		shadow(MavenPublication) { pub ->
			project.shadow.component(pub)
			
			artifact allSourcesJar
            artifact allJavadocJar

            pom {
                name = 'BDeploy Public API'
                description = 'Public API including dependencies, ready to be used for integrations and plugins.'
                url = 'https://bdeploy.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/bdeployteam/bdeploy.git'
                    developerConnection = 'scm:git:git@github.com:bdeployteam/bdeploy.git'
                    url = 'https://github.com/bdeployteam/bdeploy'
                }
                developers {
                    developer {
                        id = 'mduft'
                        name = 'Markus Duft'
                    }
                    developer {
                        id = 'mheiss'
                        name = 'Michael Heiss'
                    }
                    developer {
                        id = 'turlaub'
                        name = 'Thomas Urlaub'
                    }
                }
            }
		}
	}
	
	repositories {
		mavenLocal()
        maven {
            credentials {
                username project.findProperty('sonatypeUser') ?: 'anonymous'
                password project.findProperty('sonatypeToken') ?: ''
            }

            if(project.version.endsWith('-SNAPSHOT')) {
                url 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
        }
	}
}

if(!project.version.endsWith('-SNAPSHOT')) {
    signing {
        sign publishing.publications.shadow
    }
}

