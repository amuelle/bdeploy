plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
}

ext.installRetype = { task -> 
    configure(task) {
        group = BasePlugin.BUILD_GROUP
        if (!org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            commandLine (["npm", "install", "retypeapp"])
        } else {
            commandLine (["npm.cmd", "install", "retypeapp"])
        }
    }
}

ext.buildRetype = { task ->
    configure(task) {
        group = BasePlugin.BUILD_GROUP
	    if (!org.gradle.internal.os.OperatingSystem.current().isWindows()) {
	        commandLine (["npx", "retypeapp", "build"])
	    } else {
	        commandLine (["npx.cmd", "retypeapp", "build"])
	    }
    }
}

task retypeDevDoc(type: Exec) { t ->
    workingDir './retypedev'
    installRetype(t)
    buildRetype(t)
}

task retypeUserDoc(type: Exec) { t ->
    workingDir './retypeuser'
    installRetype(t)
    buildRetype(t)
}

task developerDoc(type: org.asciidoctor.gradle.jvm.AsciidoctorTask) {
    sourceDir = file('dev')

    sources {
        include 'index.adoc'
    }

    resources {
        from("${projectDir}/css") {
            include '*.css'
        }
        into '.'

        from(sourceDir) {
            include 'images/**'
        }
        from("${projectDir}/common") {
            include '*/**'
        }
    }

    outputDir = "${buildDir}/docs/dev"
    baseDirFollowsSourceDir()
}

task userDoc(type: org.asciidoctor.gradle.jvm.AsciidoctorTask) {
    sourceDir = file('user')

    sources {
        include 'index.adoc'
    }

    resources {
        from("${projectDir}/css") {
            include '*.css'
        }
        into '.'

        from(sourceDir) {
            include 'images/**'
        }
        from("${projectDir}/common") {
            include '*/**'
        }
    }
 
    outputDir = "${buildDir}/docs/user"
    baseDirFollowsSourceDir()
}

task userDocPdf(type: org.asciidoctor.gradle.jvm.pdf.AsciidoctorPdfTask) {
    sourceDir = file('user')

    sources {
        include 'index.adoc'
    }

    outputDir = "${buildDir}/docs/userPdf"
    baseDirFollowsSourceDir()
}

asciidoctorj {
    // stylesheets are copied AFTER the build, so ignore warning about missing stylesheet.
    attributes 'toc2': '',
               'linkcss': '',
               'stylesheet': 'plain.css'

    modules {
       diagram.version '1.5.18'
    }
}

build.dependsOn retypeDevDoc
build.dependsOn retypeUserDoc
// build.dependsOn developerDoc
// build.dependsOn userDoc
