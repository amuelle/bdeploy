plugins {
  id 'application'
}

ext {
    scriptDefVmArgs = [ '-Xms64m', '-Xmx1g' ]
}

mainClassName = 'io.bdeploy.minion.cli.MinionServerCli'

dependencies {
    implementation(
        project(":common"),
        project(":jersey"),
        project(":bhive"),
        project(":dcu"),
        project(":pcu"),
        project(":interfaces"),
        project(":ui"),

        'org.bouncycastle:bcpkix-jdk15on:1.65',
        'org.bouncycastle:bcprov-jdk15on:1.65.01',
        'org.quartz-scheduler:quartz:2.3.2',
        'net.jsign:jsign-core:3.1'
    )

    testCompile project(path: ":jersey", configuration: 'testRuntime')
    testCompile project(path: ":bhive", configuration: 'testRuntime')
    testCompile project(path: ":pcu", configuration: 'testRuntime')
    testCompile project(path: ":ui", configuration: 'testRuntime')
    testCompile project(path: ":launcher", configuration: 'testRuntime')
}

rootProject.addMultiPlatformDistBuild(project)

startScripts {
    applicationName = 'bdeploy'
}

evaluationDependsOn(':launcher')
void addLauncherDist(Distribution dist) {
    if(!project(':launcher').linux64DistZip.enabled) {
        println("WARNING: Linux launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').linux64DistZip) {
                into 'launcher'
            }
        }
    }
    if(!project(':launcher').win64DistZip.enabled) {
        println("WARNING: Windows launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').win64DistZip) {
                into 'launcher'
            }
        }
    }
    if(!project(':launcher').mac64DistZip.enabled) {
        println("WARNING: MacOS launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').mac64DistZip) {
                into 'launcher'
            }
        }
    }
}

distributions {
    win64 {
        baseName = 'bdeploy-win64'
        addLauncherDist(it)
    }
    linux64 {
        baseName = 'bdeploy-linux64'
        addLauncherDist(it)
    }
    mac64 {
        baseName = 'bdeploy-mac64'
        addLauncherDist(it)
    }
}


